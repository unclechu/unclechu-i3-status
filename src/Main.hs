-- Author: Viacheslav Lotsmanov
-- License: GPLv3 https://raw.githubusercontent.com/unclechu/unclechu-i3-status/master/LICENSE

{-# LANGUAGE PackageImports, UnicodeSyntax, LambdaCase, TupleSections #-}
{-# LANGUAGE OverloadedStrings, QuasiQuotes #-}
{-# LANGUAGE DuplicateRecordFields #-}
{-# LANGUAGE ScopedTypeVariables #-}
{-# LANGUAGE BangPatterns #-}
{-# LANGUAGE ViewPatterns #-}

module Main (main) where

import "data-default" Data.Default (def)
import "base"         Data.Bool (bool)
import "base"         Data.Word (Word8)
import "base"         Data.Tuple (swap)
import "base"         Data.Fixed (Pico)
import "base"         Data.Maybe (fromMaybe)
import "aeson"        Data.Aeson (encode, decodeStrict)
import "bytestring"   Data.ByteString.Char8 (hGetLine, uncons)
import "bytestring"   Data.ByteString.Lazy.Char8 (ByteString, append)
import "time"         Data.Time.Clock (UTCTime)

import "time"         Data.Time.LocalTime
                        ( TimeZone
                        , TimeOfDay (todSec)
                        , LocalTime (localTimeOfDay)
                        , ZonedTime ( zonedTimeToLocalTime
                                    , zonedTimeZone
                                    )

                        , getZonedTime
                        , zonedTimeToUTC
                        , utcToZonedTime
                        )

import "qm-interpolated-string" Text.InterpolatedString.QM (qms)

import "base" Control.Monad (when, forever)
import "base" Control.Concurrent (forkIO, threadDelay)
import "base" Control.Concurrent.MVar (newEmptyMVar, putMVar, takeMVar)

import "base" System.IO (stdin)
import "base" System.Exit (die, exitSuccess)

import "unix" System.Posix.Signals ( installHandler
                                   , Handler (Catch)
                                   , sigHUP
                                   , sigINT
                                   , sigTERM
                                   , sigPIPE
                                   )

import "X11"  Graphics.X11.Types ( xK_Num_Lock
                                 , xK_Caps_Lock
                                 , xK_Shift_L
                                 , xK_Shift_R
                                 )

import "X11"  Graphics.X11.Xlib (openDisplay, closeDisplay)

import "dbus" DBus ( Signal (signalBody, signalSender, signalDestination)
                   , signal
                   , IsVariant (fromVariant)
                   , variantType
                   , Type (TypeBoolean, TypeWord8)
                   )

import "dbus" DBus.Client ( connectSession
                          , disconnect
                          , requestName
                          , releaseName
                          , RequestNameReply (NamePrimaryOwner)
                          , addMatch
                          , removeMatch
                          , matchAny
                          , emit
                          , MatchRule ( matchPath
                                      , matchDestination
                                      , matchInterface
                                      , matchMember
                                      )
                          )

-- local imports

import Utils
import X (initThreads, fakeKeyEvent)
import ParentProc (dieWithParent)
import Battery (setUpBatteryIndicator)
import Types ( State (..)
             , ProtocolInitialization (..)
             , Unit (..)
             , ClickEvent (..)
             , XmonadrcIfaceParams (..)
             , XlibKeysHackIfaceParams (..)
             , UPowerBatteryState (..)
             )


view ‚à∑ State ‚Üí ByteString
view s = encode $
  [ numLockView
  , capsLockView
  , alternativeView
  , _separate
  , kbdLayoutView
  , _separate
  , dateAndTimeView
  ] ‚óá maybe mempty (\x ‚Üí [_separate, batteryView x]) (battery s)

  where numLockView, capsLockView, alternativeView, kbdLayoutView ‚à∑ Unit
        dateAndTimeView, _separate ‚à∑ Unit

        numLockView = let isOn = numLock s
          in def { fullText = "num"
                 , color    = Just $ bool "#999999" "#eeeeee" isOn
                 , name     = Just "numlock"
                 }

        capsLockView = let isOn = capsLock s
          in def { fullText = bool "caps" "CAPS" isOn
                 , color    = Just $ bool "#999999" "#ff9900" isOn
                 , name     = Just "capslock"
                 }

        alternativeView = let isOn = alternative s
          in def { fullText = bool "hax" "HAX" isOn
                 , color    = Just $ bool "#999999" "#ffff00" isOn
                 , name     = Just "alternative"
                 }

        kbdLayoutView

          | kbdLayout s ‚àà [0, 1] = let isRU = kbdLayout s ‚â¢ 0
            in def { fullText = bool "US" "RU" isRU
                   , color    = Just $ bool "#ff0000" "#00ff00" isRU
                   , name     = Just "kbdlayout"
                   }

          | otherwise = def { fullText = "%ERROR%", color = Just "#ff0000" }

        dateAndTimeView =
          fromMaybe def { fullText = "‚Ä¶" } $ set ‚àò render <$> lastTime s
          where render = renderDate ‚àò uncurry utcToZonedTime ‚àò swap
                set x  = def { fullText = x, name = Just "datentime" }

        batteryView (chargeLeft, batteryState) = def
          { -- Rounding because floating point is always zero
            fullText = icon ‚óá show (round chargeLeft ‚à∑ Word8) ‚óá "%"

          , name     = Just "battery"

          , color    = Just
                     $ case batteryState of
                            Charging     ‚Üí connectedToAdapterColor
                            FullyCharged ‚Üí connectedToAdapterColor
                            _ | chargeLeft ‚â• 80 ‚Üí "#00ff00"
                              | chargeLeft < 20 ‚Üí "#ff0000"
                              | otherwise       ‚Üí "#ffff00"

          } where connectedToAdapterColor = "#00ffff"
                  dischargingIcon = "üîã"
                  chargingIcon    = "‚ö°"

                  icon = case batteryState of
                              Charging     ‚Üí chargingIcon
                              FullyCharged ‚Üí chargingIcon
                              _            ‚Üí dischargingIcon


        _separate = def { fullText = "/", color = Just "#666666" }
        -- separateAfter x = x { separator           = Just True
        --                     , separatorBlockWidth = Just 20
        --                     }


fetchDateAndTime ‚à∑ IO (Pico, UTCTime, TimeZone)
fetchDateAndTime = getZonedTime <&> \zt ‚Üí

  let utc      = zonedTimeToUTC zt
      timeZone = zonedTimeZone  zt
      seconds  = todSec $ localTimeOfDay $ zonedTimeToLocalTime $ zt
      secsLeft = 60 - seconds -- left to next minute

   in (secsLeft, utc, timeZone)


handleClickEvent ‚à∑ IO () ‚Üí ClickEvent ‚Üí IO ()
handleClickEvent tglAlt ((\x ‚Üí name (x ‚à∑ ClickEvent)) ‚Üí Just x) = case x of

  "numlock"     ‚Üí fakeKeyEvent $ map (xK_Num_Lock,)  [False, True, False]
  "capslock"    ‚Üí fakeKeyEvent $ map (xK_Caps_Lock,) [False, True, False]
  "datentime"   ‚Üí spawnProc "gnome-calendar" []
  "alternative" ‚Üí tglAlt

  "kbdlayout"   ‚Üí fakeKeyEvent $
                    let reducer s acc = (xK_Shift_L, s) : (xK_Shift_R, s) : acc
                     in foldr reducer [] [False, True, False]

  _             ‚Üí pure ()

handleClickEvent _ _ = pure ()


main ‚à∑ IO ()
main = do
  initThreads

  -- Connecting to DBus
  client  ‚Üê connectSession

  -- Getting bus name for our service that depends on Display name
  dpyView ‚Üê do dpy    ‚Üê openDisplay ""
               let !x = getDisplayName dpy
               x <$ closeDisplay dpy

  -- Grab the bus name for our service
  requestName client (busName (def ‚à∑ XmonadrcIfaceParams) dpyView) []
    >>= \reply ‚Üí
          when (reply ‚â¢ NamePrimaryOwner) $
            die [qms| Requesting name
                      '{busName (def ‚à∑ XmonadrcIfaceParams) dpyView}'
                      error: {reply} |]

  mVar ‚Üê newEmptyMVar

  let put = putMVar mVar

      basicMatchRule = matchAny
        { matchPath        = Just $ objPath (def ‚à∑ XmonadrcIfaceParams)
        , matchInterface   = Just $ interfaceName (def ‚à∑ XmonadrcIfaceParams)
        , matchDestination = Just $ busName (def ‚à∑ XmonadrcIfaceParams) dpyView
        }

  -- If `xlib-keys-hack` started before ask it to reflush indicators
  emit client ( signal (flushObjPath  (def ‚à∑ XmonadrcIfaceParams) dpyView)
                       (interfaceName (def ‚à∑ XmonadrcIfaceParams))
                       "request_flush_all"
              ) { signalSender =
                    Just $ busName (def ‚à∑ XmonadrcIfaceParams) dpyView
                , signalDestination = Nothing
                , signalBody = []
                }

  -- Bind IPC events handlers
  sigHandlers ‚Üê
    let listen (member, stateModifier) =
          addMatch client (matchRule member) $ handle stateModifier

        matchRule member = basicMatchRule { matchMember = Just member }
        flag = fromMaybe False . fromVariant
        num  = fromMaybe 0     . fromVariant

        handle stateModifier (signalBody ‚Üí [x]) = case variantType x of
          TypeBoolean ‚Üí put $ Just $ stateModifier x
          TypeWord8   ‚Üí put $ Just $ stateModifier x
          _           ‚Üí pure () -- Incorrect arguments, just ignoring it

        -- Incorrect arguments, just ignoring it
        handle _ _ = pure ()

     in mapM listen

             -- Pairs of IPC method and state modifir
             [ ("numlock",     \x s ‚Üí s { numLock     = flag x })
             , ("capslock",    \x s ‚Üí s { capsLock    = flag x })
             , ("alternative", \x s ‚Üí s { alternative = flag x })
             , ("xkblayout",   \x s ‚Üí s { kbdLayout   = num  x })
             ]

  -- Fetcing date and time thread
  _ ‚Üê forkIO $ forever $ do
    (secondsLeftToNextMinute, utc, timeZone) ‚Üê fetchDateAndTime
    put $ Just $ \s ‚Üí s { lastTime = Just (utc, timeZone) }
    threadDelay $ ceiling $ secondsLeftToNextMinute √ó 1000 √ó 1000

  !batteryData ‚Üê setUpBatteryIndicator $ \case
    (Nothing, Nothing) ‚Üí pure ()

    (Just chargeLeft, Just chargeState) ‚Üí
      put $ Just $ \s ‚Üí s { battery = Just (chargeLeft, chargeState) }

    (Just chargeLeft, Nothing) ‚Üí
      put $ Just $ \s ‚Üí s
        { battery = snd <$> battery s >>= Just ‚àò (chargeLeft,) }

    (Nothing, Just chargeState) ‚Üí
      put $ Just $ \s ‚Üí s
        { battery = fst <$> battery s >>= Just ‚àò (,chargeState) }

  let handleEv = handleClickEvent $
        emit client ( signal (objPath (def ‚à∑ XlibKeysHackIfaceParams))
                             (interfaceName (def ‚à∑ XlibKeysHackIfaceParams))
                             "toggle_alternative_mode"
                    ) { signalSender =
                          Just $ busName (def ‚à∑ XmonadrcIfaceParams) dpyView
                      , signalDestination =
                          Just $ busName (def ‚à∑ XlibKeysHackIfaceParams) dpyView
                      , signalBody = []
                      }

  -- Reading click events from i3-bar
  _ ‚Üê forkIO $ do
    !"[" ‚Üê hGetLine stdin -- Opening of lazy list
    do -- First one (without comma)
      Just ev ‚Üê decodeStrict <$> hGetLine stdin
      handleEv ev
    forever $ do
      Just ev ‚Üê (\(uncons ‚Üí Just (',', x)) ‚Üí decodeStrict x) <$> hGetLine stdin
      handleEv ev

  -- Handle POSIX signals to terminate application
  let terminate = do fromMaybe (pure ()) $ snd <$> batteryData -- unsubscribe
                     mapM_ (removeMatch client) sigHandlers
                     _ ‚Üê releaseName client
                       $ busName (def ‚à∑ XmonadrcIfaceParams) dpyView
                     disconnect client
                     put Nothing

      catch sig = installHandler sig (Catch terminate) Nothing

   in mapM_ catch [sigHUP, sigINT, sigTERM, sigPIPE]

  dieWithParent -- make this app die if parent die

  echo $ encode (def ‚à∑ ProtocolInitialization) { clickEvents = True }
  echo "[" -- Opening of lazy list

  -- Main thread is reactive loop that gets state modifier from another thread
  -- to update the state and re-render it (if it's Just) or terminate the
  -- application (it it's Nothing).
  let handle ‚à∑ State ‚Üí Maybe (State ‚Üí State) ‚Üí IO State
      handle prevState Nothing = prevState <$ echo "]" >> exitSuccess

      handle prevState (Just stateModifier) =
        let newState = stateModifier prevState
         in if newState ‚â° prevState
               then pure prevState
               else newState <$ echo ("," `append` view newState)

      next s = takeMVar mVar >>= handle s >>= next

      defState = case fst <$> batteryData of
                      Nothing ‚Üí def
                      x       ‚Üí def { battery = x }

   in () <$ echo (view defState) >> next defState
